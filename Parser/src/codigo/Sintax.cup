package codigo;

import java_cup.runtime.Symbol;

parser code
{:
    public Symbol s;
    
    /**
     * 
     * Método al que se llama automáticamente ante algún error sintactico.
     **/ 
    public void syntax_error(Symbol s){ 
        if (s.value != null){
            Pantalla.errores += "Error Sintáctico en la Línea " + (s.left) +
            " Columna "+s.right+ ". No se esperaba este componente: " +s.value+"\n\r"; 
        }
    } 

    /**
     * Método al que se llama automáticamente ante algún error sintáctico 
     * en el que ya no es posible una recuperación de errores.
     **/ 
    public void unrecovered_syntax_error(Symbol s) throws java.lang.Exception{ 
        System.out.println("Error síntactico irrecuperable en la Línea " + 
        (s.left)+ " Columna "+s.right+". Componente " + s.value + 
        " no reconocido"); 
    } 

    public Symbol getS(){
        return this.s;
    }
:};

terminal Coma, Case, Break, Const, Continue, Default, Do, Else, For, If, Return,
    Switch, _Void, While, TDato, Op_Bin, Op_Una, Puntoycoma, Asignacion,
    Parentesis_Izquierdo, Parentesis_Derecho, 
    Corchete_Izquierdo, Corchete_Derecho, Dos_Puntos, Read, Write, Identificador, Entero, Caracter;

non terminal INICIO, DECLARACION, DECLARACION2, LISTA_ID, LISTA_ID_P, DATO, DECLARACION_IMPLEMENTACION, DECLARACION_IMPLEMENTACION2,
    FUNCION, FUNCION_P, FUNCION_P2, DECLARACION_SIMPLE, LISTA_SIM, STMT, STMT_S, RETURN, ASIGNACION,
    EXPRESION, EXPRESION_P, EXPRESION_P2, READ, WRITE, LISTA_EXP, LISTA_EXP_P,
    IF, IF_ELSE, ELSE, FOR, FOR_P, WHILE, SWITCH, DO_WHILE, LLAMADA_FUNCION,LISTA_DATOS, LISTA_DATOS_P, LLAMADA_FUNCION_P;

start with INICIO;

INICIO ::= 
    DECLARACION INICIO| 
    DECLARACION_IMPLEMENTACION Puntoycoma INICIO | 
    FUNCION INICIO  
;


DECLARACION ::= 
    TDato:t LISTA_ID Puntoycoma {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSDecla(); :}|
    Const TDato:t LISTA_ID Puntoycoma {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSDecla(); :}|
    error LISTA_ID Puntoycoma |
    error TDato LISTA_ID Puntoycoma |
    Const error LISTA_ID Puntoycoma
;

DECLARACION2 ::= 
    TDato:t LISTA_ID Puntoycoma {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSDecla2(); :}|
    Const TDato:t LISTA_ID Puntoycoma {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSDecla2(); :}|
    error LISTA_ID Puntoycoma |
    error TDato LISTA_ID Puntoycoma |
    Const error LISTA_ID Puntoycoma
;

LISTA_ID ::= 
    Identificador:i LISTA_ID_P {: Semantic.recuerdaId((String) i); :} |
    error LISTA_ID_P
;

LISTA_ID_P ::= 
    Coma Identificador:d LISTA_ID_P {: Semantic.recuerdaId((String) d); :}|
    error Identificador LISTA_ID_P |
    Coma error LISTA_ID_P |
;



DECLARACION_IMPLEMENTACION ::=
    TDato:t Identificador:i Asignacion EXPRESION {:Semantic.recuerdaTipo((String) t); Semantic.recuerdaId((String) i); Semantic.recuerdaImplementacion(); 
    Semantic.evalBinary(); Semantic.asignarValor((String) i); :}|
    Const TDato:t Identificador:i Asignacion EXPRESION {:Semantic.recuerdaTipo((String) t); Semantic.recuerdaId((String) i); Semantic.recuerdaImplementacion(); 
    Semantic.evalBinary(); Semantic.asignarValor((String) i); :}|

    error Identificador Asignacion EXPRESION |
    TDato error Asignacion EXPRESION 
;

DECLARACION_IMPLEMENTACION2 ::=
    TDato:t Identificador:i Asignacion EXPRESION {:Semantic.recuerdaTipo((String) t); Semantic.recuerdaId((String) i); Semantic.recuerdaImplementacion2(); 
    Semantic.evalBinary(); Semantic.asignarValor((String) i); :}|
    Const TDato:t Identificador:i Asignacion EXPRESION {:Semantic.recuerdaTipo((String) t); Semantic.recuerdaId((String) i); Semantic.recuerdaImplementacion2(); 
    Semantic.evalBinary(); Semantic.asignarValor((String) i); :}|

    error Identificador Asignacion EXPRESION |
    TDato error Asignacion EXPRESION 
;


LISTA_DATOS ::= 
    Identificador LISTA_DATOS_P |
    Entero LISTA_DATOS_P |
    Caracter LISTA_DATOS_P  
;

LISTA_DATOS_P ::= 
    Coma Identificador LISTA_DATOS_P |
    Coma Entero LISTA_DATOS_P |
    Coma Caracter LISTA_DATOS_P |
    error Identificador LISTA_DATOS_P |
    Coma error LISTA_DATOS_P |
;

FUNCION ::=
    _Void:t FUNCION_P {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSFunc(); :} |
    TDato:t FUNCION_P {: Semantic.recuerdaTipo((String) t); Semantic.recuerdaTSFunc(); :} |
    error FUNCION_P
;

FUNCION_P ::= 
    FUNCION_P2| FUNCION FUNCION_P2 
;

FUNCION_P2 ::= Identificador:i Parentesis_Izquierdo DECLARACION_SIMPLE 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho {: Semantic.recuerdaId((String) i); :}|

    error Parentesis_Izquierdo DECLARACION_SIMPLE 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    Identificador error DECLARACION_SIMPLE 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    Identificador Parentesis_Izquierdo DECLARACION_SIMPLE 
    error Corchete_Izquierdo STMT Corchete_Derecho |

    Identificador Parentesis_Izquierdo DECLARACION_SIMPLE 
    Parentesis_Derecho error STMT Corchete_Derecho 
;

LLAMADA_FUNCION ::= Identificador Parentesis_Izquierdo LISTA_DATOS Parentesis_Derecho
                    
;

LLAMADA_FUNCION_P ::= Identificador Parentesis_Izquierdo Parentesis_Derecho 

;

DECLARACION_SIMPLE ::= 
    TDato Identificador LISTA_SIM |
    TDato error LISTA_SIM |
;

LISTA_SIM ::=
    Coma TDato Identificador LISTA_SIM |

    Coma error Identificador LISTA_SIM |
    Coma TDato error LISTA_SIM |
;


STMT ::= 
    DECLARACION2 STMT | 
    DECLARACION_IMPLEMENTACION2 Puntoycoma STMT |
    ASIGNACION Puntoycoma STMT |
    IF STMT |
    IF_ELSE STMT|
    WHILE STMT |
    FOR STMT |
    DO_WHILE |
    SWITCH STMT |
    Break Puntoycoma STMT |
    Continue Puntoycoma STMT |
    READ STMT |
    WRITE STMT |
    LLAMADA_FUNCION Puntoycoma STMT|
    LLAMADA_FUNCION_P Puntoycoma STMT |
    Identificador Puntoycoma STMT |
    Identificador Op_Una Puntoycoma STMT |
    RETURN |
    error |
;

RETURN ::= 
    Return EXPRESION Puntoycoma
;


ASIGNACION ::=
    Identificador:i Asignacion EXPRESION {: Semantic.evalBinary(); Semantic.asignarValor((String) i); :}|
    error
;


IF ::= 
    If Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho {: Semantic.evalBinary(); Semantic.startIf(); Semantic.testIf(); Semantic.endIf(); :}| 

    error Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |
    If error EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |
    If Parentesis_Izquierdo EXPRESION 
    error Corchete_Izquierdo STMT Corchete_Derecho |
    If Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho error STMT Corchete_Derecho     
;

IF_ELSE ::= 
    If Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho ELSE |

    error Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho ELSE |
    If error EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho ELSE |
    If Parentesis_Izquierdo EXPRESION 
    error Corchete_Izquierdo STMT Corchete_Derecho ELSE |
    If Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho error STMT Corchete_Derecho ELSE     
;


ELSE ::=
    Else Corchete_Izquierdo STMT Corchete_Derecho {: Semantic.evalBinary(); Semantic.startIf(); Semantic.testIfE();  Semantic.startElse(); Semantic.endIf(); :}| 

    Else error STMT Corchete_Derecho 
;


WHILE ::= 
    While Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho {: Semantic.evalBinary(); Semantic.startWhile(); Semantic.testWhile(); Semantic.endWhile(); :}|
    While error EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |
    While Parentesis_Izquierdo EXPRESION 
    error Corchete_Izquierdo STMT Corchete_Derecho |
    While Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho error STMT Corchete_Derecho 
;


FOR ::= 
    For Parentesis_Izquierdo EXPRESION Puntoycoma EXPRESION Puntoycoma EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    For error FOR_P Puntoycoma EXPRESION Puntoycoma EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    For Parentesis_Izquierdo EXPRESION error EXPRESION Puntoycoma EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    For Parentesis_Izquierdo EXPRESION Puntoycoma EXPRESION error EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT Corchete_Derecho |

    For Parentesis_Izquierdo EXPRESION Puntoycoma EXPRESION Puntoycoma EXPRESION 
    error Corchete_Izquierdo STMT Corchete_Derecho |

    For Parentesis_Izquierdo EXPRESION Puntoycoma EXPRESION Puntoycoma EXPRESION 
    Parentesis_Derecho error STMT Corchete_Derecho 
;




DO_WHILE ::=
    Do Corchete_Izquierdo STMT Corchete_Derecho
    While Parentesis_Izquierdo EXPRESION Parentesis_Derecho Puntoycoma |

    Do error STMT Corchete_Derecho
    While Parentesis_Izquierdo EXPRESION Parentesis_Derecho Puntoycoma |
    Do Corchete_Izquierdo STMT Corchete_Derecho
    While Parentesis_Izquierdo EXPRESION error Puntoycoma |
    Do Corchete_Izquierdo STMT Corchete_Derecho
    While Parentesis_Izquierdo EXPRESION Parentesis_Derecho error 
;


SWITCH ::= 
    Switch Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT_S Corchete_Derecho |

    error Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT_S Corchete_Derecho |
    Switch error EXPRESION 
    Parentesis_Derecho Corchete_Izquierdo STMT_S Corchete_Derecho |
    Switch Parentesis_Izquierdo EXPRESION 
    error Corchete_Izquierdo STMT_S Corchete_Derecho |
    Switch Parentesis_Izquierdo EXPRESION 
    Parentesis_Derecho error STMT_S Corchete_Derecho 
;


STMT_S ::=
    Case EXPRESION Dos_Puntos STMT STMT_S |
    Default Dos_Puntos STMT |

    Case EXPRESION error STMT STMT_S |
    Default error STMT 
;


READ ::=
    Read Parentesis_Izquierdo Identificador Parentesis_Derecho Puntoycoma |
    Read Parentesis_Izquierdo Parentesis_Derecho Puntoycoma |

    Read error Identificador Parentesis_Derecho Puntoycoma |
    Read Parentesis_Izquierdo error Parentesis_Derecho Puntoycoma |
    Read Parentesis_Izquierdo Identificador error Puntoycoma |
    Read Parentesis_Izquierdo Identificador Parentesis_Derecho error |
    Read error Parentesis_Derecho Puntoycoma |
    Read Parentesis_Izquierdo error Puntoycoma |
    Read Parentesis_Izquierdo Parentesis_Derecho error 
;


WRITE ::=
    Write Parentesis_Izquierdo LISTA_EXP Parentesis_Derecho Puntoycoma |

    Write error LISTA_EXP Parentesis_Derecho Puntoycoma |
    Write Parentesis_Izquierdo LISTA_EXP error Puntoycoma |
    Write Parentesis_Izquierdo LISTA_EXP Parentesis_Derecho error 
;


LISTA_EXP ::= 
    EXPRESION LISTA_EXP_P
;

LISTA_EXP_P ::=
    Coma EXPRESION LISTA_EXP_P |
;



EXPRESION ::= 
    Parentesis_Izquierdo EXPRESION Parentesis_Derecho EXPRESION_P|
    Identificador:i EXPRESION_P {: Semantic.recuerdaVarExp((String) i); :}|
    Caracter EXPRESION_P {: Semantic.recuerdaConstExp("1"); :}|
    Entero:i EXPRESION_P {: Semantic.recuerdaConstExp((String) i); :}|
    LLAMADA_FUNCION EXPRESION_P |
    LLAMADA_FUNCION_P EXPRESION_P |
    LLAMADA_FUNCION Op_Una EXPRESION_P |
    LLAMADA_FUNCION_P Op_Una EXPRESION_P |
    Identificador:i Op_Una EXPRESION_P |
    Caracter Op_Una EXPRESION_P |
    Entero:i Op_Una EXPRESION_P |
    
    Parentesis_Izquierdo EXPRESION error |
    error Op_Una EXPRESION_P |
    error EXPRESION_P 
;

EXPRESION_P ::= 
    Op_Bin:o EXPRESION {: Semantic.recuerdaOperExp( (String) o); :}|
    Asignacion EXPRESION |
    
;
